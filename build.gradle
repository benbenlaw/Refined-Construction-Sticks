import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.165'
    id 'net.darkhax.curseforgegradle' version '1.1.24'
    id "com.modrinth.minotaur" version "2.+"

}

version = "${minecraft_version}-${mod_version}"
group = mod_group_id

repositories {
    mavenLocal()

    maven {
        name = "Curse Maven"
        url = uri("https://cursemaven.com")
        content {
            includeGroup("curse.maven")
        }
    }
}
base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        arguments '--nogui'  // change here
    }
    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        arguments '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}


dependencies {

    implementation "net.neoforged:neoforge:${neo_version}"

    implementation "curse.maven:jei-238222:6614392" // JEI
    implementation "curse.maven:refined-storage-243076:7039043" // refined storage
    implementation "curse.maven:construction-sticks-1156098:7687867" // construction sticks
    implementation "curse.maven:guideme-1173950:6812025" // guide me
    implementation "curse.maven:storage-drawers-223852:6995432" // storage drawers

}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {

        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }

        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    group "modpublishing"

    apiToken = System.getenv("curseforge_token").toString()

    def projectId = 1460013
    def mainFile = upload(projectId, jar)

    mainFile.changelog = changelog
    mainFile.addOptional("jei") // JEI
    mainFile.releaseType = "release"
    mainFile.gameVersions.addAll(minecraft_version)
    mainFile.addEnvironment("server", "client")
}

tasks.getByName("modrinth").setGroup("modpublishing")

modrinth {
    token = System.getenv("modrinth_token").toString()
    projectId = "G4DDLIRm"
    versionType = "release"
    versionName = "refinedsticks-" + "$minecraft_version" + "-" + "$mod_version"
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!

    gameVersions.addAll(minecraft_version)

    loaders = ["neoforge"]
    changelog = project.changelog
    dependencies {
        optional.project "jei"
    }
}





